const HDLD_PIPEGUN="pip";
//////////////////////////////////////////////
//The original blueprints of the pipegun are unsure, but most regard them to be something with disgruntled potato farmers off the world of Bloke IV.
//Now 3D printed and sold across the galaxy, it is an ideal weapon of criminals everywhere, easily stowed, as simple as a pipe with a trigger and a dream,
//you too can murder your ex-partner in crime with this "weapon"!
//////////////////////////////////////////////
class HDPipeGunSG:HDWeapon{
	default{
		//$Category "Weapons/Hideous Destructor"
		//$Title "Raycob Exterminator"
		//$Sprite "TRMPA0"
		+hdweapon.fitsinbackpack
		+hdweapon.reverseguninertia
		scale 0.63;
		weapon.selectionorder 50;
		weapon.slotnumber 3;
		weapon.slotpriority 0.5;
		weapon.kickback 30;
		weapon.bobrangex 0.1;
		weapon.bobrangey 0.6;
		weapon.bobspeed 2.5;
		weapon.bobstyle "normal";
		obituary "%o wasn't aware that %k's dysfunctional tube would be able to kill.";
		inventory.pickupmessage "You got a modified version of potatobloke's pipegun!";
		tag "Pipegun";
		hdweapon.refid HDLD_PIPEGUN;
		hdweapon.barrelsize 21,0.3,0.5;
		inventory.maxamount 3;
	}
	
		override string pickupmessage(){
		return super.pickupmessage();
	}
		double shotpower;
	static clearscope double getshotpower(){return frandom(0.9,1.05);}
	
		//returns the power of the load just fired
	static double Fire(actor caller){
		double spread=10.; //Spread was 6 but Ted said it was a bit much, so why not make it 10?
		double speedfactor=1.;
		let ttt=HDPipeGunSG(caller.findinventory("HDPipeGunSG"));
		if(ttt){
			spread=6.5-0.5*6;
			speedfactor=1.+0.02857*30;
		}

		double shotpower=getshotpower();
		spread*=shotpower;
		speedfactor*=shotpower;
		HDBulletActor.FireBullet(caller,"HDB_wad");
		let p=HDBulletActor.FireBullet(caller,"HDB_00",
			spread:spread,speedfactor:speedfactor,amount:7
		);
		distantnoise.make(p,"world/shotgunfar");
		caller.A_StartSound("weapons/hunter",CHAN_WEAPON);
		return shotpower;
	}
	action void A_FireTerminator(){
		double shotpower=invoker.Fire(self);
		A_GunFlash();
		vector2 shotrecoil=(randompick(-1,1),-2.6);
		A_MuzzleClimb(0,0,shotrecoil.x,shotrecoil.y,randompick(-1,1)*shotpower,-0.3*shotpower);
		invoker.weaponstatus[TRMI_CHAMBER]=1;
		invoker.shotpower=shotpower;
	}
	

	
	override bool AddSpareWeapon(actor newowner){return AddSpareWeaponRegular(newowner);}
	override hdweapon GetSpareWeapon(actor newowner,bool reverse){return GetSpareWeaponRegular(newowner,reverse);}
	override double weaponbulk(){
		return 45;
	}
	override double gunmass(){
		return 7;
	}
	override string,double getpickupsprite(){
		string spr;
		if(weaponstatus[TRMI_CHAMBER]<1){
			spr="B";
		}else{
			spr="A";
		}
		return "TRMP"..spr.."0",1.;
	}
	override void DrawHUDStuff(HDStatusBar sb,HDWeapon hdw,HDPlayerPawn hpl){
		if(sb.hudlevel == 1){
			sb.drawimage("SHL1A0",(-47,-10),basestatusbar.DI_SCREEN_CENTER_BOTTOM);
			sb.drawnum(hpl.countinv("HDShellAmmo"),-46,-8,
				basestatusbar.DI_SCREEN_CENTER_BOTTOM
			);
		}
		if(hdw.weaponstatus[TRMI_CHAMBER]>1){
			sb.drawwepdot(-19,-11,(5,3));
			sb.drawwepdot(-16,-11,(2,3));
		}
		}
		
	override string gethelptext(){
		return
		WEPHELP_FIRESHOOT
		..WEPHELP_RELOADRELOAD
		..WEPHELP_UNLOADUNLOAD
		;
	}
	override void DrawSightPicture(
		HDStatusBar sb,HDWeapon hdw,HDPlayerPawn hpl,
		bool sightbob,vector2 bob,double fov,bool scopeview,actor hpc,string whichdot
	){
		int cx,cy,cw,ch;
		[cx,cy,cw,ch]=screen.GetClipRect();
		vector2 scc;
		vector2 bobb=bob*1.6;

		if(hpl.player.getpsprite(PSP_WEAPON).frame>=2){
			sb.SetClipRect(
				-10+bob.x,-5+bob.y,20,14,
				sb.DI_SCREEN_CENTER
			);
			scc=(0.7,0.8);
			bobb.y=clamp(bobb.y*1.1-3,-10,10);
		}else{
			sb.SetClipRect(
				-8+bob.x,-4+bob.y,16,10,
				sb.DI_SCREEN_CENTER
			);
			scc=(0.6,0.6);
			bobb.y=clamp(bobb.y,-16,16);
		}
		sb.drawimage(
			"ctrmsite",(0,0)+bobb,sb.DI_SCREEN_CENTER|sb.DI_ITEM_TOP,
			alpha:0.9,scale:scc
		);
		sb.SetClipRect(cx,cy,cw,ch);
		sb.drawimage(
			"TNT1A0",(0,0)+bob,sb.DI_SCREEN_CENTER|sb.DI_ITEM_TOP,
			scale:scc
		);
	}
	override void DropOneAmmo(int amt){
		if(owner){
			amt=clamp(amt,1,10);
			if(owner.countinv("HDShellAmmo"))owner.A_DropInventory("HDShellAmmo",amt*15);
		}
	}
	states{
	select0:
		TRMG A 0 A_JumpIf(invoker.weaponstatus[TRMI_CHAMBER]>0,2);
		TRMG C 0;
		---- # 1 A_Raise();
		---- # 1 A_Raise(30);
		---- # 1 A_Raise(30);
		---- # 1 A_Raise(24);
		---- # 1 A_Raise(18);
		wait;
	deselect0:
		TRMG A 0 A_JumpIf(invoker.weaponstatus[TRMI_CHAMBER]>0,2);
		TRMG C 0;
		---- ### 1 A_Lower();
		---- # 1 A_Lower(18);
		---- # 1 A_Lower(24);
		---- # 1 A_Lower(30);
		wait;
	ready:
		TRMG A 0 A_JumpIf(invoker.weaponstatus[TRMI_CHAMBER]>1,2);
		TRMG B 0;
		TRMG # 1{
			A_SetCrosshair(21);
			A_WeaponReady(WRF_ALL);
		}goto readyend;
	user2:
	firemode:
		goto nope;
	altfire:
		---- A 0;
		goto nope;
	althold:
	hold:
		goto nope;
	fire:
		---- A 0{
			invoker.weaponstatus[0]&=~TRMF_JUSTUNLOAD;
			if(invoker.weaponstatus[TRMI_CHAMBER]==2)setweaponstate("shoot");
		}goto nope;
	shoot:
		TRMG B 1{
            if(invoker.weaponstatus[TRMI_CHAMBER]==2)
            A_FireTerminator();
            //givebody(max(0,11-health));
            damagemobj(invoker,self,5,"bashing");
            A_StartSound("weapons/terminatorfwd",7,0,0.9);
        }
		TRMG D 1{
			if(hdplayerpawn(self)){
				hdplayerpawn(self).gunbraced=false;
				A_GiveInventory("IsMoving",1);
			}
				A_MuzzleClimb(1.6,-3.9,1.6,-3.9);
		}
		TRMG D 0{
			//invoker.weaponstatus[TRMI_CHAMBER]=1;
				A_StartSound("weapons/terminatoropen",7,0,0.9);
				setweaponstate("nope");
			}

		TRMG D 1{
			A_WeaponReady(WRF_NOFIRE);
			invoker.weaponstatus[TRMI_CHAMBER]=2;
			invoker.weaponstatus[TRMI_MAG]--;
			A_Refire();
		}goto ready;
	flash:
		TRMF A 1 bright{
			HDFlashAlpha(64);
			A_Light1();
			//invoker.weaponstatus[TRMI_CHAMBER]=1;
			A_ZoomRecoil(0.995);
			A_MuzzleClimb(-frandom(0.4,1.2),-frandom(0.4,1.6));
		}
		TRMF A 0 A_StartSound("weapons/terminatorfire",CHAN_WEAPON,0,1.0);
		TRMF A 0 A_Light0();
		stop;
	unload:
		---- A 0{
			invoker.weaponstatus[0]|=TRMF_JUSTUNLOAD;
			if(invoker.weaponstatus[TRMI_CHAMBER]<=1)setweaponstate("nope");
		}
		goto continueloadchamber;
	loadchamber:
		---- A 0{
			if(
				!(invoker.weaponstatus[0]&TRMF_JUSTUNLOAD)
				&&(
					invoker.weaponstatus[TRMI_CHAMBER]==2
				)
			)setweaponstate("nope");
		}
		
		TRMC A 0 {A_JumpIf(invoker.weaponstatus[TRMF_JUSTUNLOAD],"continueloadchamber");}
		TRMC AB 2;
		TRMC C 2 A_StartSound("weapons/terminatoropen",7,0,0.9);
		TRMC DE 2;
	continueloadchamber:
		---- B 0{
			if(
				!(invoker.weaponstatus[0]&TRMF_JUSTUNLOAD)
				&&(
					invoker.weaponstatus[TRMI_CHAMBER]==2
				)
			)setweaponstate("nope");
		}
		---- "#" 1 offset(2,36) A_ClearRefire();
		---- "#" 1 offset(3,38);
		---- "#" 1 offset(5,42);
		---- "#" 1 offset(8,48) A_StartSound("weapons/pocket",CHAN_WEAPON);
		---- "#" 1 offset(9,52) A_MuzzleClimb(frandom(-0.2,0.2),0.2,frandom(-0.2,0.2),0.2,frandom(-0.2,0.2),0.2);
		---- "#" 2 offset(8,60);
		---- "#" 2 offset(7,72);
		TNT1 "#" 6;
		TNT1 "#" 4; //A_StartSound("weapons/terminatoropen",CHAN_WEAPON);
		TNT1 "#" 9 A_StartSound("weapons/pocket",CHAN_WEAPON);
		TNT1 "#" 6 {
			A_StartSound("weapons/bossload",CHAN_WEAPON,0,0.7);
			if(invoker.weaponstatus[0]&TRMF_JUSTUNLOAD){
				int chm=invoker.weaponstatus[TRMI_CHAMBER];
				invoker.weaponstatus[TRMI_CHAMBER]=0;
				if(chm<2||A_JumpIfInventory("HDShellAmmo",0,"null")){
					class<actor> whatkind=chm==1?"HDShellAmmo":"HDSpentShell_Sweeper";
					actor rrr=spawn(whatkind,pos+(cos(angle)*10,sin(angle)*10,height-12),ALLOW_REPLACE);
					rrr.angle=angle;rrr.A_ChangeVelocity(1,2,1,CVF_RELATIVE);
				}else HDF.Give(self,"HDShellAmmo",1);
			}else{
				A_TakeInventory("HDShellAmmo",1,TIF_NOTAKEINFINITE);
				invoker.weaponstatus[TRMI_CHAMBER]=2;
			}
		} 
		TRMG "#" 0{
			if(invoker.weaponstatus[TRMI_CHAMBER]<=1)setweaponstate("emptyraise");
		}
		TRMC E 2 offset(7,72);
		TRMC "#" 2 offset(8,60);
		TRMC "#" 1 offset(7,52);
		TRMC "#" 1 offset(5,42);
		TRMC "#" 1 offset(3,38);
		TRMC "#" 1 offset(3,35);
		TRMC "#" 0;//{
			//if(invoker.weaponstatus[TRMI_CHAMBER]>=1)setweaponstate("recock");
		//}
		goto ready;
		emptyraise:
		TNT1 A 3;
		TNT1 A 9 A_StartSound("weapons/terminatorfwd",7,0,0.9);
		TRMG B 2 offset(7,72);
		TRMG "#" 2 offset(8,60);
		TRMG "#" 1 offset(7,52);
		TRMG "#" 1 offset(5,42);
		TRMG "#" 1 offset(3,38);
		TRMG "#" 1 offset(3,35);
		goto ready;
		
	reload:
		---- A 0{
			invoker.weaponstatus[0]&=~TRMF_JUSTUNLOAD;
			if(invoker.weaponstatus[TRMI_CHAMBER]>=1)setweaponstate("nope");
			{
				if(countinv("HDShellAmmo"))setweaponstate("loadchamber");
				else setweaponstate("nope");
			}
		}
			goto nope;
	spawn:
		TRMP AB -1 nodelay{
			if(invoker.weaponstatus[TRMI_CHAMBER]<2){
				frame=1;
			}else{
				frame=0;
			}
		}stop;
	}
	override void initializewepstats(bool idfa){
		weaponspecial = 1337;
		weaponstatus[0]=0;
		weaponstatus[TRMI_CHAMBER]=2;
	}
	override void loadoutconfigure(string input){}
}
/*class TerminatorRandom:IdleDummy{
	states{
	spawn:
		TNT1 A 0 nodelay{
			let lll=HDPipeGunSG(spawn("HDPipeGunSG",pos,ALLOW_REPLACE));
			if(!lll)return;
			lll.special=special;
			lll.vel=vel;
//			for(int i=0;i<5;i++)lll.args[i]=args[i];
		}stop;
	}
}*/

enum terminatorstatus{
	TRMF_JUSTUNLOAD=1,

	TRMI_FLAGS=0,
	TRMI_MAG=1,
	TRMI_CHAMBER=2, //0 empty, 1 spent, 2 loaded
};

class PipeGunInjector:EventHandler
{
	override void CheckReplacement(ReplaceEvent e)
	{
		if (!e.Replacement)
		{
			return;
		}

		switch (e.Replacement.GetClassName())
		{
			case 'ShellBoxRandom':
				if (random[rumrandom]() <= 24)
				{
					e.Replacement = "HD_PipegunDropper";
					e.IsFinal = true;
				}
				break;
		}
	}

	override void WorldThingSpawned(WorldEvent e)
	{
		let PipeAmmo = HDAmmo(e.Thing);
		if (!PipeAmmo)
		{
			return;
		}

		switch (PipeAmmo.GetClassName())
		{
			case 'HDShellAmmo':
				PipeAmmo.ItemsThatUseThis.Push("HDPipeGunSG");
				break;
		}
	}
}

class HD_PipegunDropper:IdleDummy{
    states{
    spawn:
        TNT1 A 0 nodelay{
			A_SpawnItemEx("ShellBoxPickup",frandom(0,12),frandom(0,12),frandom(0,12),0,0,0,0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx("HDPipeGunSG",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
        }stop;
    }
}