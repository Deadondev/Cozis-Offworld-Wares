// ------------------------------------------------------------
// Commando .355 Carbine
// ------------------------------------------------------------

const HDLD_COLT355_NORM="c35";
const HDLD_COLT355_MAG="35m";

class HD_Colt355:HDWeapon{
	default{
		//$Category "Weapons/Hideous Destructor"
		//$Title "Commando .355 Carbine"
		//$Sprite "C9MPA0"

		+hdweapon.fitsinbackpack
		obituary "%o was soaked by %k's pea stream.";
		weapon.selectionorder 24;
		weapon.slotnumber 2;
		weapon.kickback 30;
		weapon.bobrangex 0.3;
		weapon.bobrangey 0.6;
		weapon.bobspeed 2.5;
		scale 0.65;
		inventory.pickupmessage "You got the Commando!";
		hdweapon.barrelsize 28,0.5,1;
		hdweapon.refid HDLD_COLT355_NORM;
		tag "Commando .355 Carbine";
	}
	override bool AddSpareWeapon(actor newowner){return AddSpareWeaponRegular(newowner);}
	override hdweapon GetSpareWeapon(actor newowner,bool reverse,bool doselect){return GetSpareWeaponRegular(newowner,reverse,doselect);}
	//override double gunmass(){
	//	return 5+(weaponstatus[COLT9S_MAG]<0)?-0.5:(weaponstatus[COLT9S_MAG]*0.02);
	//}
	
	override double gunmass(){
			return 5.5+(weaponstatus[COLT9S_MAG]<0)?-0.5:(weaponstatus[COLT9S_MAG]*0.02);
	}
	
	//override double weaponbulk(){
	//	int mg=weaponstatus[COLT9S_MAG];
	//	if(mg<0)return 95;
	//	else return (95+ENC_9MAG30_LOADED)+mg*ENC_355_LOADED;
	//}
	
	override void postbeginplay(){
		super.postbeginplay();
			barrellength=28;
			barrelwidth=0.5;
			barreldepth=1;
	}
	
	override double weaponbulk(){
		double blx=95;
		int mgg=weaponstatus[COLT9S_MAG];
		return blx+(mgg<0?0:((ENC_9MAG30_LOADED)+mgg*ENC_355_LOADED));
	}
	
	override void failedpickupunload(){
		failedpickupunloadmag(COLT9S_MAG,"HDColt355Mag25");
	}
	override void DropOneAmmo(int amt){
		if(owner){
			amt=clamp(amt,1,10);
			if(owner.countinv("HDRevolverAmmo"))owner.A_DropInventory("HDRevolverAmmo",amt*25);
			else owner.A_DropInventory("HDColt355Mag25",amt);
		}
	}
	override void ForceBasicAmmo(){
		owner.A_TakeInventory("HDRevolverAmmo");
		owner.A_TakeInventory("HDColt355Mag25");
		owner.A_GiveInventory("HDColt355Mag25");
	}
	override string,double getpickupsprite(){
		return "C9MP"..((weaponstatus[COLT9S_MAG]<0)?"B":"A").."0",1.;
	}
	override void DrawHUDStuff(HDStatusBar sb,HDWeapon hdw,HDPlayerPawn hpl){
		if(sb.hudlevel==1){
			int nextmagloaded=sb.GetNextLoadMag(hdmagammo(hpl.findinventory("HDColt355Mag25")));
			if(nextmagloaded>=25){
				sb.drawimage("CLP3A0",(-46,-3),sb.DI_SCREEN_CENTER_BOTTOM,scale:(3,3));
			}else if(nextmagloaded<1){
				sb.drawimage("CLP3B0",(-46,-3),sb.DI_SCREEN_CENTER_BOTTOM,alpha:nextmagloaded?0.6:1.,scale:(3,3));
			}else sb.drawbar(
				"CLP3NORM","CLP3GREY",
				nextmagloaded,25,
				(-46,-3),-1,
				sb.SHADER_VERT,sb.DI_SCREEN_CENTER_BOTTOM
			);
			sb.drawnum(hpl.countinv("HDColt355Mag25"),-43,-8,sb.DI_SCREEN_CENTER_BOTTOM,font.CR_BLACK);
		}
		sb.drawwepcounter(hdw.weaponstatus[COLT9S_AUTO],
			-22,-10,"RBRSA3A7","STFULAUT","STFULAUT"
		);
		sb.drawwepnum(hdw.weaponstatus[COLT9S_MAG],25);
		if(hdw.weaponstatus[COLT9S_CHAMBER]==2)sb.drawwepdot(-16,-10,(3,1));
	}
	override string gethelptext(){
		return
		WEPHELP_FIRESHOOT
		..WEPHELP_RELOAD.."  Reload\n"
		..WEPHELP_FIREMODE.."  Semi/Auto\n"
		..WEPHELP_MAGMANAGER
		..WEPHELP_UNLOADUNLOAD
		;
	}
	override void DrawSightPicture(
		HDStatusBar sb,HDWeapon hdw,HDPlayerPawn hpl,
		bool sightbob,vector2 bob,double fov,bool scopeview,actor hpc,string whichdot
	){
		int cx,cy,cw,ch;
		[cx,cy,cw,ch]=screen.GetClipRect();
		sb.SetClipRect(
			-16+bob.x,-4+bob.y,32,16,
			sb.DI_SCREEN_CENTER
		);
		vector2 bobb=bob*3;
		bobb.y=clamp(bobb.y,-8,8);
		sb.drawimage(
			"CLT9_ST1",(0,-3)+bobb,sb.DI_SCREEN_CENTER|sb.DI_ITEM_TOP,
			alpha:0.9,scale:(0.75,0.75)
		);
		sb.SetClipRect(cx,cy,cw,ch);
		sb.drawimage(
			"backsite",(0,0)+bob,sb.DI_SCREEN_CENTER|sb.DI_ITEM_TOP
		);
	}
	states{
	select0:
		C9MG A 0;
		#### A 0;
		goto select0small;
	deselect0:
		C9MG A 0;
		#### A 0;
		goto deselect0small;

	ready:
		C9MG A 0;
		#### A 0;
		#### A 1{
			A_SetCrosshair(21);
			A_WeaponReady(WRF_ALL);
		}
		goto readyend;
	user3:
		---- A 0 A_MagManager("HDColt355Mag25");
		goto ready;
	altfire:
	althold:
		goto nope;
	hold:
		#### A 0{
			if(
				//full auto
				invoker.weaponstatus[COLT9S_AUTO]==1
			)setweaponstate("fire2");
			else setweaponstate("nope");
		}goto fire;
	user2:
	firemode:
		---- A 1{
			int aut=invoker.weaponstatus[COLT9S_AUTO];
			if(aut>=1)invoker.weaponstatus[COLT9S_AUTO]=0;
			else if(aut<0)invoker.weaponstatus[COLT9S_AUTO]=0;
			else invoker.weaponstatus[COLT9S_AUTO]++;
		}goto nope;
	fire:
		C9MG A 0;
		#### A 1;
	fire2:
		#### B 1{
			if(invoker.weaponstatus[COLT9S_CHAMBER]==2){
				A_GunFlash();
				A_WeaponOffset(frandom(-3,3),frandom(30,33));
			}else{
				if(invoker.weaponstatus[COLT9S_MAG]>0)setweaponstate("chamber");
				else setweaponstate("nope");
			}
		}
		#### A 1;
		#### A 0{
			if(invoker.weaponstatus[COLT9S_CHAMBER]==1){
				A_SpawnItemEx("HDSpent355",
					cos(pitch)*10,0,height-10-sin(pitch)*10,
					vel.x,vel.y,vel.z,
					0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
				);
				invoker.weaponstatus[COLT9S_CHAMBER]=0;
			}
			if(invoker.weaponstatus[COLT9S_MAG]>0){
				invoker.weaponstatus[COLT9S_MAG]--;
				invoker.weaponstatus[COLT9S_CHAMBER]=2;
			}
			if(invoker.weaponstatus[COLT9S_AUTO]==1)A_SetTics(1);
			A_WeaponReady(WRF_NOFIRE);
		}
		#### A 0 A_ReFire();
		goto readyend;
	flash:
		C9MF A 0{
			let bbb=HDBulletActor.FireBullet(self,"HDB_355",speedfactor:1.1);
			if(
				frandom(32,ceilingz-floorz)<bbb.speed*0.2
			)A_AlertMonsters(200);

			A_ZoomRecoil(1.15);
			A_StartSound("weapons/smg/355",CHAN_WEAPON,0,1.1);
			invoker.weaponstatus[COLT9S_CHAMBER]=1;
		}
		C9MF A 1 bright{
			HDFlashAlpha(15);
			A_Light1();
		}
		#### A 0 A_MuzzleClimb(-frandom(0.35,0.4),-frandom(0.45,0.5),-frandom(0.35,0.35),-frandom(0.4,0.45)); //SMG's is (-frandom(0.2,0.24),-frandom(0.3,0.36),-frandom(0.2,0.24),-frandom(0.3,0.36)); The OG Commando's is (-frandom(0.6,0.8),-frandom(0.7,1.0),-frandom(0.6,0.8),-frandom(0.7,1.0)); so i went middle
		goto lightdone;


	unloadchamber:
		C9MG A 0;
		#### F 2 offset(2,34) A_JumpIf(invoker.weaponstatus[COLT9S_CHAMBER]<1,"nope");
		#### G 2 offset(5,40) A_StartSound("weapons/smg/355/cock",CHAN_6,0,0.5);
		#### H 3 offset(9,47){
			class<actor>which=invoker.weaponstatus[COLT9S_CHAMBER]>1?"HDRevolverAmmo":"HDSpent355";
			invoker.weaponstatus[COLT9S_CHAMBER]=0;
			A_SpawnItemEx(which,
				cos(pitch)*10,0,height-8-sin(pitch)*10,
				vel.x,vel.y,vel.z,
				0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
			);
		}
		#### G 2 offset(5,37);
		#### F 2 offset(2,36);
		goto readyend;
	loadchamber:
		---- A 0 A_JumpIf(invoker.weaponstatus[COLT9S_CHAMBER]>0,"nope");
		---- A 0 A_JumpIf(!countinv("HDRevolverAmmo"),"nope");
		---- A 1 offset(0,34) A_StartSound("weapons/pocket",CHAN_WEAPON);
		---- A 1 offset(2,36);
		#### B 1 offset(5,40);
		#### B 4 offset(4,39){
			if(countinv("HDRevolverAmmo")){
				A_TakeInventory("HDRevolverAmmo",1,TIF_NOTAKEINFINITE);
				invoker.weaponstatus[COLT9S_CHAMBER]=2;
				A_StartSound("weapons/smg/355/fwd",CHAN_WEAPON);
			}
		}
		#### B 7 offset(5,37);
		#### B 1 offset(2,36);
		#### A 1 offset(0,34);
		goto readyend;
	user4:
	unload:
		C9MG A 0;
		#### A 0{
			invoker.weaponstatus[0]|=COLT9F_JUSTUNLOAD;
			if(
				invoker.weaponstatus[COLT9S_MAG]>=0
			)setweaponstate("unmag");
			else if(invoker.weaponstatus[COLT9S_CHAMBER]>0)setweaponstate("unloadchamber");
		}goto nope;
	reload:
		C9MG A 0;
		#### A 0{
			invoker.weaponstatus[0]&=~COLT9F_JUSTUNLOAD;
			if(invoker.weaponstatus[COLT9S_MAG]>=25)setweaponstate("nope");
			else if(HDMagAmmo.NothingLoaded(self,"HDColt355Mag25")){
				if(
					invoker.weaponstatus[COLT9S_MAG]<0
					&&countinv("HDRevolverAmmo")
				)setweaponstate("loadchamber");
				else setweaponstate("nope");
			}
		}goto unmag;
	unmag:
		C9MG A 0;
		#### A 1 offset(0,34) A_SetCrosshair(21);
		#### A 1 offset(5,38);
		#### A 1 offset(10,42);
		#### C 2 offset(20,46) A_StartSound("weapons/smgmagclick",CHAN_WEAPON);
		#### D 4 offset(30,52){
			A_MuzzleClimb(0.3,0.4);
			A_StartSound("weapons/smgmagmove",CHAN_WEAPON);
		}
		#### E 0{
			int magamt=invoker.weaponstatus[COLT9S_MAG];
			if(magamt<0){
				setweaponstate("magout");
				return;
			}
			invoker.weaponstatus[COLT9S_MAG]=-1;
			if(
				(!PressingUnload()&&!PressingReload())
				||A_JumpIfInventory("HDColt355Mag25",0,"null")
			){
				HDMagAmmo.SpawnMag(self,"HDColt355Mag25",magamt);
				setweaponstate("magout");
			}else{
				HDMagAmmo.GiveMag(self,"HDColt355Mag25",magamt);
				A_StartSound("weapons/pocket",CHAN_WEAPON);
				setweaponstate("pocketmag");
			}
		}
	pocketmag:
		#### EE 7 offset(34,54) A_MuzzleClimb(frandom(0.2,-0.8),frandom(-0.2,0.4));
	magout:
		#### E 0{
			if(invoker.weaponstatus[0]&COLT9F_JUSTUNLOAD)setweaponstate("reloadend");
			else setweaponstate("loadmag");
		}

	loadmag:
		#### E 0 A_StartSound("weapons/pocket",CHAN_WEAPON);
		#### E 6 offset(34,54) A_MuzzleClimb(frandom(0.2,-0.8),frandom(-0.2,0.4));
		#### E 7 offset(34,52) A_MuzzleClimb(frandom(0.2,-0.8),frandom(-0.2,0.4));
		#### E 10 offset(32,50);
		#### E 3 offset(32,49){
			let mmm=hdmagammo(findinventory("HDColt355Mag25"));
			if(mmm){
				invoker.weaponstatus[COLT9S_MAG]=mmm.TakeMag(true);
				A_StartSound("weapons/smgmagclick",CHAN_BODY);
			}
			if(
				invoker.weaponstatus[COLT9S_MAG]<1
				||invoker.weaponstatus[COLT9S_CHAMBER]>0
			)setweaponstate("reloadend");
		}
	chamber:
		C9MG A 0;
		#### E 2 offset(33,50){
			invoker.weaponstatus[COLT9S_MAG]--;
			invoker.weaponstatus[COLT9S_CHAMBER]=2;
		}
		#### E 4 offset(32,49) A_StartSound("weapons/smg/355/fwd",CHAN_WEAPON);

	reloadend:
		#### E 3 offset(30,52);
		#### E 2 offset(20,46);
		#### D 1 offset(10,42);
		#### C 1 offset(5,38);
		#### A 1 offset(0,34);
		goto nope;


	spawn:
		C9MP A -1 nodelay{
			if(invoker.weaponstatus[COLT9S_MAG]<0)frame=1;
		}
	}
	override void initializewepstats(bool idfa){
		weaponstatus[0]=0;
		weaponstatus[COLT9S_MAG]=25;
		weaponstatus[COLT9S_CHAMBER]=2;
		if(!idfa)weaponstatus[COLT9S_AUTO]=0;
	}
	override void loadoutconfigure(string input){
		int firemode=getloadoutvar(input,"firemode",1);
		if(firemode>0)weaponstatus[COLT9S_AUTO]=clamp(firemode,0,1);
	}
}

class HDColt355Mag25:HD9mmag15{
	default{
		//$Category "Ammo/Hideous Destructor/"
		//$Title "SMG Magazine"
		//$Sprite "CLP3A0"
		
		scale 0.65;
		hdmagammo.roundtype "HDRevolverAmmo";
		hdmagammo.roundbulk ENC_355_LOADED;
		hdmagammo.maxperunit 25;
		hdmagammo.magbulk ENC_9MAG30_EMPTY;
		tag "M35A1 HPC Magazine";
		inventory.pickupmessage "Picked up an M35A1 HPC magazine.";
		hdpickup.refid HDLD_COLT355_MAG;
	}
	override string,string,name,double getmagsprite(int thismagamt){
		string magsprite=(thismagamt>0)?"C9MAA0":"C9MAB0";
		return magsprite,"PBRSA0","HDRevolverAmmo",2.;
	}
	override void GetItemsThatUseThis(){
		itemsthatusethis.push("HD_Colt355");
	}
	states{
	spawn:
		C9MA A -1;
		stop;
	spawnempty:
		C9MA B -1{
			brollsprite=true;brollcenter=true;
			roll=randompick(0,0,0,0,2,2,2,2,1,3)*90;
		}stop;
	}
}

class HDColt355Mag25_Empty:IdleDummy{
	override void postbeginplay(){
		super.postbeginplay();
		weaponspecial = 1337; // UaS sling compatibility.
		HDMagAmmo.SpawnMag(self,"HDColt355Mag25",0);
		destroy();
	}
}

enum colt355status{
	COLT9F_JUSTUNLOAD=1,
	COLT9F_UNDERSLUNG=2,

	COLT9S_FLAGS=0,
	COLT9S_MAG=1,
	COLT9S_CHAMBER=2, //0 empty, 1 spent, 2 loaded
	COLT9S_AUTO=3, //0 semi, 1 auto
	COLT9S_SG_TUBE=4, //3 rounds
	COLT9S_SG_HAND=5,
	COLT9S_SG_CHAMBER=6,
};

class Colt355Injector:EventHandler{
override void WorldThingSpawned(WorldEvent e) { 
        let ColtAmmo = HDAmmo(e.Thing);     
     if (ColtAmmo){             
  switch (ColtAmmo.GetClassName()){
  case 'HDRevolverAmmo': ColtAmmo.ItemsThatUseThis.Push("HD_Colt355");                     break;                 
        }
        }
         }     
} 